if (rootProject.ext.isPictureApplication) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //阿里路由框架配置
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }

        if (rootProject.ext.isPictureApplication) {
            applicationId "com.github.stormwyrm.picture"
        }
//        multiDexEnabled true
    }

    resourcePrefix 'picture_'

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            if (rootProject.ext.isPictureApplication) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //正式版本时，排除alone文件夹下所有调试文件
                    exclude 'src/main/module/*'
                }
            }
        }
    }
}

dependencies {
    implementation rootProject.ext.dependencies['kotlin']
    implementation project(':library')
    kapt rootProject.ext.dependencies['arouter-compiler']
}